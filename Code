//Vẫn đang trong quá trình viết, thử nghiệm và cải tiến chức năng
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <Keypad.h>

// LCD I2C
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Keypad 4x4
const byte ROWS = 4;
const byte COLS = 4;
char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {10, 11, 12, 13};
byte colPins[COLS] = {A0, A1, A2, A3};
Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

// Người dùng và mật khẩu
const int USER_COUNT = 4;
String userNames[USER_COUNT] = {"A", "B", "C", "D"};
String passwords[USER_COUNT] = {"1234", "5678", "2468", "1357"};
int currentUser = 0;
int activatedBy = -1; // << Lưu người đã kích hoạt

// PIR sensors
const int pirCount = 5;
const int pirPins[pirCount] = {2, 3, 4, 5, 6};

// LED & Buzzer
const int ledPin = 9;
const int buzzerPin = 8;

// Trạng thái hệ thống
bool alarmActive = false;
bool alarmTriggered = false;

// Nhập mật khẩu
String inputPass = "";

// Nhấp nháy
unsigned long lastBlink = 0;
bool ledState = false;
const unsigned long blinkInterval = 300;

// Quét PIR + Debounce
int motionCounter = 0;
unsigned long lastMotionTime = 0;
const int motionThreshold = 2;
const unsigned long motionResetTime = 1000;
unsigned long lastPirScan = 0;
const unsigned long pirInterval = 1000;

void setup() {
  lcd.init();
  lcd.backlight();
  pinMode(ledPin, OUTPUT);
  pinMode(buzzerPin, OUTPUT);
  for (int i = 0; i < pirCount; i++) pinMode(pirPins[i], INPUT);
  Serial.begin(9600);
  showUserPrompt();
}

void loop() {
  if (!alarmActive) {
    getPasswordInput();
  } else {
    monitorSensors();
    if (alarmTriggered) {
      blinkAlarm();
      getPasswordToDeactivate();
    }
  }
}

// ------------ HIỂN THỊ ------------
void showUserPrompt() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Nhap Pass ");
  lcd.print(userNames[currentUser]);
  lcd.setCursor(0, 1);
}

void showPasswordStars(String pass) {
  lcd.setCursor(0, 1);
  for (int i = 0; i < pass.length(); i++) lcd.print('*');
  for (int i = pass.length(); i < 8; i++) lcd.print(' ');
}

// ------------ KÍCH HOẠT HỆ THỐNG ------------
void getPasswordInput() {
  char key = keypad.getKey();
  if (key) {
    if (key >= 'A' && key <= 'D') {
      currentUser = key - 'A';
      inputPass = "";
      showUserPrompt();
    } else if (key == '#') {
      if (inputPass == passwords[currentUser]) {
        lcd.clear();
        lcd.print("Dang kich hoat...");
        delay(1000);
        alarmActive = true;
        activatedBy = currentUser; // << Ghi nhớ người kích hoạt
        lcd.clear();
        lcd.print("He thong chay");
        delay(1000);
        lcd.clear();
      } else {
        lcd.clear();
        lcd.print("Sai pass!");
        delay(1000);
        showUserPrompt();
        inputPass = "";
      }
    } else if (key == '*') {
      inputPass = "";
      showUserPrompt();
    } else if (inputPass.length() < 8) {
      inputPass += key;
      showPasswordStars(inputPass);
    }
  }
}

// ------------ GIÁM SÁT PIR ------------
void monitorSensors() {
  unsigned long now = millis();
  if (alarmTriggered) return;

  if (now - lastPirScan >= pirInterval) {
    lastPirScan = now;
    bool motionDetected = false;

    for (int i = 0; i < pirCount; i++) {
      if (digitalRead(pirPins[i]) == HIGH) {
        motionDetected = true;
        motionCounter++;
        lastMotionTime = now;
        Serial.print("PIR "); /*Serial.print(i);*/ Serial.println(" co chuyen dong");
        break;
      }
    }

    if (motionDetected && motionCounter >= motionThreshold) {
      alarmTriggered = true;
      motionCounter = 0;
      lcd.clear();
      lcd.print("CANH BAO! XAM NHAP");
      digitalWrite(ledPin, HIGH);
      digitalWrite(buzzerPin, HIGH);
      lastBlink = now;
    }

    if (!motionDetected && (now - lastMotionTime > motionResetTime)) {
      motionCounter = 0;
    }
  }
}

// ------------ NHẤP NHÁY CẢNH BÁO ------------
void blinkAlarm() {
  unsigned long now = millis();
  if (now - lastBlink >= blinkInterval) {
    lastBlink = now;
    ledState = !ledState;
    digitalWrite(ledPin, ledState);
    digitalWrite(buzzerPin, ledState);
  }
}

// ------------ TẮT HỆ THỐNG ------------
void getPasswordToDeactivate() {
  static String deactPass = "";
  char key = keypad.getKey();
  if (key) {
    if (key >= 'A' && key <= 'D') {
      currentUser = key - 'A';
      deactPass = "";
      lcd.clear();
      lcd.print("Tat Pass ");
      lcd.print(userNames[currentUser]);
      lcd.setCursor(0, 1);
    } else if (key == '#') {
      if (currentUser == activatedBy && deactPass == passwords[currentUser]) {
        lcd.clear();
        lcd.print("Da tat he thong");
        delay(1000);
        alarmActive = false;
        alarmTriggered = false;
        digitalWrite(ledPin, LOW);
        digitalWrite(buzzerPin, LOW);
        motionCounter = 0;
        ledState = false;
        deactPass = "";
        inputPass = "";
        activatedBy = -1; // Reset
        showUserPrompt();
      } else {
        lcd.clear();
        lcd.print("Sai pass hoac");
        lcd.setCursor(0, 1);
        lcd.print("khong co quyen");
        delay(1500);
        lcd.clear();
        lcd.print("Tat Pass ");
        lcd.print(userNames[currentUser]);
        lcd.setCursor(0, 1);
        deactPass = "";
      }
    } else if (key == '*') {
      deactPass = "";
      lcd.setCursor(0, 1);
      lcd.print("                ");
    } else if (deactPass.length() < 8) {
      deactPass += key;
      showPasswordStars(deactPass);
    }
  }
}
